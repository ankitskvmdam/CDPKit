# -*- mode: CMake -*-

##
# InstallExternalRuntimeDependencies.cmake
#
# This file is part of the Chemical Data Processing Toolkit
#
# Copyright (C) 2003-2020 Thomas A. Seidel <thomas.seidel@univie.ac.at>
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2 of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with this program; see the file COPYING. If not, write to
# the Free Software Foundation, Inc., 59 Temple Place - Suite 330,
# Boston, MA 02111-1307, USA.
##


IF(WIN32)
  SET(DEPENDENCY_INSTALL_DIR "@CDPKIT_EXECUTABLE_INSTALL_DIR@")
ELSE(WIN32)
  SET(DEPENDENCY_INSTALL_DIR "@CDPKIT_LIBRARY_INSTALL_DIR@")
ENDIF(WIN32)

IF(COMMAND CMAKE_POLICY)
  CMAKE_POLICY(SET CMP0011 NEW)
  CMAKE_POLICY(SET CMP0007 NEW)
ENDIF(COMMAND CMAKE_POLICY)

MACRO(INSTALL_DEPENDENCIES BINARY_INPUT_FILE)
  SET(EXCLUDE_PATTERNS
    "^libcdpl.*"
    "^.*linux.*"
    "^libGL.*"
    "^libX.*"
    "^libpython.*"
    "^.*system32.*"
    "^.*ms-win-core.*"
    )

  GET_FILENAME_COMPONENT(FILE_SUFFIX "${BINARY_INPUT_FILE}" LAST_EXT)

  IF(WIN32)
    STRING(REPLACE ":" ";" SEARCH_DIRS "$ENV{PATH}")
    STRING(REPLACE "C;\\" "C:\\" SEARCH_DIRS "${SEARCH_DIRS}")
    STRING(REPLACE "C;/" "C:/" SEARCH_DIRS "${SEARCH_DIRS}")
  ELSE()
    SET(SEARCH_DIRS "")
  ENDIF(WIN32)

  IF("${FILE_SUFFIX}" STREQUAL "" OR "${FILE_SUFFIX}" STREQUAL ".exe")
    FILE(GET_RUNTIME_DEPENDENCIES RESOLVED_DEPENDENCIES_VAR DEPENDENCIES UNRESOLVED_DEPENDENCIES_VAR UNRESOLVED_DEPS
         EXECUTABLES "${BINARY_INPUT_FILE}" PRE_EXCLUDE_REGEXES ${EXCLUDE_PATTERNS} POST_EXCLUDE_REGEXES ${EXCLUDE_PATTERNS}
         DIRECTORIES ${SEARCH_DIRS})  
  ELSEIF("${FILE_SUFFIX}" STREQUAL ".dll" OR "${FILE_SUFFIX}" STREQUAL ".so" OR "${FILE_SUFFIX}" STREQUAL ".dylib")
    FILE(GET_RUNTIME_DEPENDENCIES RESOLVED_DEPENDENCIES_VAR DEPENDENCIES UNRESOLVED_DEPENDENCIES_VAR UNRESOLVED_DEPS
         LIBRARIES "${BINARY_INPUT_FILE}" PRE_EXCLUDE_REGEXES ${EXCLUDE_PATTERNS} POST_EXCLUDE_REGEXES ${EXCLUDE_PATTERNS}
         DIRECTORIES ${SEARCH_DIRS})  
  ELSEIF("${FILE_SUFFIX}" STREQUAL ".pyd")
    FILE(GET_RUNTIME_DEPENDENCIES RESOLVED_DEPENDENCIES_VAR DEPENDENCIES UNRESOLVED_DEPENDENCIES_VAR UNRESOLVED_DEPS
         MODULES "${BINARY_INPUT_FILE}" PRE_EXCLUDE_REGEXES ${EXCLUDE_PATTERNS} POST_EXCLUDE_REGEXES ${EXCLUDE_PATTERNS}
         DIRECTORIES ${SEARCH_DIRS})
  ENDIF()

  FOREACH(DEPENDENCY ${DEPENDENCIES})
    GET_FILENAME_COMPONENT(FILE_NAME "${DEPENDENCY}" NAME)

    IF(NOT EXISTS "${CMAKE_INSTALL_PREFIX}/${DEPENDENCY_INSTALL_DIR}/${FILE_NAME}")
      MESSAGE("-- Installing Dependency: ${DEPENDENCY}")
      CONFIGURE_FILE("${DEPENDENCY}"
        "${CMAKE_INSTALL_PREFIX}/${DEPENDENCY_INSTALL_DIR}/${FILE_NAME}"
        COPYONLY
        )
    ENDIF(NOT EXISTS "${CMAKE_INSTALL_PREFIX}/${DEPENDENCY_INSTALL_DIR}/${FILE_NAME}")
  ENDFOREACH(DEPENDENCY)

  FOREACH(DEPENDENCY ${UNRESOLVED_DEPS})
    GET_FILENAME_COMPONENT(FILE_NAME "${DEPENDENCY}" NAME)
    
    IF(NOT EXISTS "${CMAKE_INSTALL_PREFIX}/${DEPENDENCY_INSTALL_DIR}/${FILE_NAME}")
      MESSAGE("!! Unresolved Dependency: ${DEPENDENCY}")
    ENDIF()
  ENDFOREACH(DEPENDENCY)
ENDMACRO(INSTALL_DEPENDENCIES)

#STRING(REGEX MATCH "CPack" IN_CPACK_INSTALL "${CMAKE_INSTALL_PREFIX}")

#IF(NOT IN_CPACK_INSTALL)
#  RETURN()
#ENDIF(NOT IN_CPACK_INSTALL)

MESSAGE("- Processing Runtime Dependencies of: @BINARY_INPUT_FILE@")
INSTALL_DEPENDENCIES("@BINARY_INPUT_FILE@")
